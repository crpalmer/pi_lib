function(util_common targetName)
    platform_executable(${targetName})
    target_link_libraries(${targetName}
      pi-audio
      pi-core
      ${ARGN}
    )
    target_compile_definitions(${targetName} PUBLIC "CYW43_HOST_NAME=\"${targetName}\"")
endfunction()

function(util_c targetName)
    add_executable(${targetName} ${targetName}.c)
    util_common(${targetName} ${ARGN})
endfunction()

function(util_cpp targetName)
    add_executable(${targetName} ${targetName}.cpp)
    util_common(${targetName} ${ARGN})
endfunction()

if("${PLATFORM}" STREQUAL pico)
  util_cpp(ble pi-net)
  util_cpp(light-sensor)
  util_cpp(neopixel-pico)
  util_cpp(notes)
  util_cpp(rtos-test pi-threads)
  util_cpp(pico-rtc pi-net)
else()
  util_cpp(digital-counter)
  util_c(maestro)
  util_cpp(neopixel-pi)
  util_c(nes-dump)
  util_cpp(st7735s)
  util_cpp(talking-skull-dump)
endif()

util_cpp(adc)
util_cpp(gp-input)
util_cpp(life)
util_cpp(mcp23017)
util_cpp(rotary-encoder)
util_cpp(talking-skull-laugh)
util_cpp(servo)
util_c(sntp pi-net)
util_cpp(ssd1306)
util_cpp(wav-play)
